#!/usr/bin/env perl

use strict;
use warnings;

use Data::Dumper::Concise qw( Dumper );
use File::Slurp::Tiny qw( read_file write_file );

sub main {
    my $tests = _tests_from_spec();

    my $dumped = Data::Dumper->Dump( [$tests], ['$tests'] );
    $dumped = 'sub _tests { my ' . $dumped . 'return $tests; } 1;';
    write_file( './t/commonmark-data/tests.pl', $dumped );
}

sub _tests_from_spec {
    my $spec
        = read_file( 'commonmark/spec.txt', binmode => ':encoding(UTF-8)' );

    my @tests;
    while ( $spec =~ /^\.\n([\s\S]*?)^\.\n([\s\S]*?)^\.$/gm ) {
        push @tests, {
            markdown => $1,
            html     => $2,
            };
        $tests[-1]{markdown} =~ s/\x{2192}/\t/g;
    }

    return \@tests;
}

main();
